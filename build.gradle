/*
 * ******************************************************************************
 *   Copyright 2014-2017 Spectra Logic Corporation. All Rights Reserved.
 *   Licensed under the Apache License, Version 2.0 (the "License"). You may not use
 *   this file except in compliance with the License. A copy of the License is located at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   or in the "license" file accompanying this file.
 *   This file is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 *   CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *   specific language governing permissions and limitations under the License.
 * ****************************************************************************
 */

import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.10' apply false
}

allprojects {
    group = 'com.spectralogic.ds3autogen'
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    // Use a Java 17 toolchain for compilation
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        // Explicitly set compatibility on the Java extension (avoids deprecated JavaPluginConvention)
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }


    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation 'com.google.guava:guava:21.0'
        implementation 'org.freemarker:freemarker:2.3.23'
        implementation 'org.slf4j:slf4j-api:1.7.12'
        implementation 'org.slf4j:jcl-over-slf4j:1.7.12'

        testImplementation('org.mockito:mockito-core:4.11.0') {
            exclude group: 'org.hamcrest'
        }
        testImplementation 'junit:junit:4.12'
        testImplementation 'org.hamcrest:hamcrest-library:1.3'
        testImplementation 'org.slf4j:slf4j-simple:1.7.12'
        testImplementation 'org.assertj:assertj-core:3.8.0'
        testImplementation 'org.assertj:assertj-guava:3.1.0'
    }
}

tasks.named('wrapper', Wrapper) {
    gradleVersion = '8.14'
    distributionType = Wrapper.DistributionType.ALL
}

subprojects {
    plugins.withId("org.jetbrains.kotlin.jvm") {
        tasks.withType(KotlinCompile).configureEach {
            compilerOptions {
                jvmTarget.set(JvmTarget.JVM_17)
            }
        }
        // Align Kotlin toolchain with Java 17
        kotlin {
            jvmToolchain(17)
        }
    }
}

